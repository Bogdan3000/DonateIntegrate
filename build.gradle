buildscript {
    repositories {
        mavenCentral()
        maven { url "https://maven.minecraftforge.net" }
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.4'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'com.github.johnrengelman.shadow'

version = "2.0.5"
group = "com.bogdan3000.dintegrate"
archivesBaseName = "DonateIntegrate"

sourceCompatibility = targetCompatibility = '1.8'

minecraft {
    version = "1.12.2-14.23.5.2847"
    runDir = "run"
    mappings = "stable_39"
}

repositories {
    mavenCentral()
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    compile 'org.apache.logging.log4j:log4j-core:2.8.1'
    compile 'org.apache.logging.log4j:log4j-api:2.8.1'
    compile 'org.java-websocket:Java-WebSocket:1.5.3'
    compile 'com.google.code.gson:gson:2.8.2'
    compile 'org.slf4j:slf4j-api:1.7.30'
    compile 'org.slf4j:slf4j-simple:1.7.30'
    compile 'org.yaml:snakeyaml:1.33'
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

tasks.withType(JavaCompile) {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
    options.encoding = 'UTF-8'
}

runClient {
    jvmArgs '-Xmx2G', '-Xms1G', '-Dhttps.protocols=TLSv1.2'
}

runServer {
    jvmArgs '-Xmx2G', '-Xms1G', '-Dhttps.protocols=TLSv1.2'
}

shadowJar {
    classifier = ''
    dependencies {
        include(dependency('org.java-websocket:Java-WebSocket:1.5.3'))
        include(dependency('com.google.code.gson:gson:2.8.2'))
        include(dependency('org.slf4j:slf4j-api:1.7.30'))
        include(dependency('org.slf4j:slf4j-simple:1.7.30'))
        include(dependency('org.yaml:snakeyaml:1.33'))
    }
    relocate 'org.slf4j', 'com.bogdan3000.dintegrate.slf4j'
    relocate 'com.google.gson', 'com.bogdan3000.dintegrate.gson'
    relocate 'org.yaml.snakeyaml', 'com.bogdan3000.dintegrate.snakeyaml'
    relocate 'org.java_websocket', 'com.bogdan3000.dintegrate.java_websocket'
    doLast {
        println "ShadowJar completed. Output: ${archivePath}"
    }
}

reobf {
    shadowJar {}
}

jar {
    manifest {
        attributes(
                'Specification-Title': 'DonateIntegrate',
                'Specification-Version': '2.0',
                'Implementation-Title': project.name,
                'Implementation-Version': project.version
        )
    }
    finalizedBy shadowJar
}

// Отключаем задачу extractAnnotationsJar, чтобы избежать дублирования fml_cache_annotation.json
tasks.extractAnnotationsJar.enabled = false

build.dependsOn shadowJar